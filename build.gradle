version '1.40'

apply plugin: 'groovy'
apply plugin: 'application'


repositories {
    mavenCentral()
}

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.15'
    compile 'commons-cli:commons-cli:1.4'
    compile 'org.apache.ant:ant:1.9.4'
    compile 'org.fusesource.jansi:jansi:1.16'

    testCompile 'junit:junit:4.12'
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

task unzipJre (type: Copy){
        from zipTree("./lib/jre-1.8.161.zip")
        into "${buildDir}/jre"
}
jar.dependsOn unzipJre

tasks.withType(CreateStartScripts) {
    def tplName = 'src/main/resources/devtool.bat'
    assert project.file(tplName).exists()
    windowsStartScriptGenerator.template = resources.text.fromFile(tplName)
}

applicationDistribution.from("./lib") {
    include "curl.exe"
    include "curl-ca-bundle.crt"
    into("bin")
}

applicationDistribution.from("${buildDir}/jre") {
    include "**/**"
}

applicationDistribution.from(".") {
    include "changelog.md"
    include "readme.md"
    include "REGISTRY_USAGE.md"
}

applicationDistribution.from(".") {
    include "example-*.xml"
    include "example-*.json"
    into("examples")
}

mainClassName = "eu.konopski.devtool.Devtool"
